buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.9.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group = 'study.huhao.demo'
version = '1.0.0'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    compile {
        exclude module: 'spring-boot-starter-tomcat'
        exclude module: 'junit'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jersey'
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.0.1'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    annotationProcessor 'org.projectlombok:lombok'
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'ma.glasnost.orika:orika-core:1.5.4'

    // flyway 5.2.4 cannot support h2 1.4.198+
    runtimeOnly 'com.h2database:h2:1.4.197'

    implementation 'org.flywaydb:flyway-core'
    testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:5.2.1'

    // just use to show how to use REST Assured to improve test
    // should place rest-assured before the JUnit dependency declaration in order to make sure
    // that the correct version of Hamcrest is used
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.rest-assured:spring-mock-mvc'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.4.1'

    testImplementation 'com.tngtech.archunit:archunit-junit5-api:0.10.2'
    testRuntimeOnly 'com.tngtech.archunit:archunit-junit5-engine:0.10.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}
